package handle_map

import "base:builtin"
import "core:fmt"
import "core:math/linalg/glsl"
import "core:slice"
import "core:time"

Handle_Map :: struct($T: typeid, $HT: typeid) {
	handles:        [dynamic]HT,
	values:         [dynamic]T,
	sparse_indices: [dynamic]Sparse_Index,
	next:           u32,
}

Handle :: struct {
	generation: u32,
	index:      u32,
}

Nil_Handle :: Handle{max(u32), max(u32)}

Sparse_Index :: struct {
	generation:    u32,
	index_or_next: u32,
}

init :: proc(m: ^Handle_Map($T, $HT), allocator := context.allocator) {
	m.handles.allocator = allocator
	m.values.allocator = allocator
	m.sparse_indices.allocator = allocator
	m.next = 0
}

destroy :: proc(m: ^Handle_Map($T, $HT)) {
	clear(m)
	delete(m.handles)
	delete(m.values)
	delete(m.sparse_indices)
}

clear :: proc(m: ^$M/Handle_Map($T, $HT)) {
	builtin.clear(&m.handles)
	builtin.clear(&m.values)
	builtin.clear(&m.sparse_indices)
	m.next = 0
}

@(require_results)
has_handle :: proc(m: ^Handle_Map($T, $HT), h: HT) -> bool {
	if h.index < u32(len(m.sparse_indices)) {
		return m.sparse_indices[h.index].generation == h.generation
	}
	return false
}

@(require_results)
get :: proc(m: ^Handle_Map($T, $HT), h: HT) -> (^T, bool) {
	if h.index < u32(len(m.sparse_indices)) {
		entry := m.sparse_indices[h.index]
		if entry.generation == h.generation {
			return &m.values[entry.index_or_next], true
		}
	}
	return nil, false
}

@(require_results)
insert :: proc(m: ^Handle_Map($T, $HT), value: T) -> (handle: HT) {
	if m.next < u32(len(m.sparse_indices)) {
		entry := &m.sparse_indices[m.next]
		assert(entry.generation < max(u32), "Generation sparse indices overflow")

		entry.generation += 1
		handle = HT {
			generation = entry.generation,
			index      = m.next,
		}
		m.next = entry.index_or_next
		entry.index_or_next = u32(len(m.handles))
		append(&m.handles, handle)
		append(&m.values, value)
	} else {
		assert(m.next < max(u32), "Index sparse indices overflow")

		handle = HT {
			index = u32(len(m.sparse_indices)),
		}
		append(&m.sparse_indices, Sparse_Index{index_or_next = u32(len(m.handles))})
		append(&m.handles, handle)
		append(&m.values, value)
		m.next += 1
	}
	return
}

remove :: proc(m: ^Handle_Map($T, $HT), h: HT) -> (value: T, ok: bool) {
	if h.index < u32(len(m.sparse_indices)) {
		entry := &m.sparse_indices[h.index]
		if entry.generation != h.generation {
			return
		}
		index := entry.index_or_next
		entry.generation += 1
		entry.index_or_next = m.next
		m.next = h.index
		value = m.values[index]
		ok = true
		unordered_remove(&m.handles, int(index))
		unordered_remove(&m.values, int(index))
		if index < u32(len(m.handles)) {
			m.sparse_indices[m.handles[index].index].index_or_next = index
		}
	}
	return
}
